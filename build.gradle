buildscript {
	ext {
		springBootVersion = '2.7.4'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

plugins {
    id 'com.palantir.docker' version '0.22.1'
}

group = 'kretzlerdevs'

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'eclipse'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
	mavenCentral()
}

dependencies {
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	implementation 'com.auth0:java-jwt:3.18.3'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.jsoup:jsoup:1.15.3'
	testImplementation 'junit:junit:4.12'
}

springBoot {
	mainClass = "org.kpmp.Application"
}

jar {
	baseName='user-auth'
	version='1.0'
}

task unpack(type: Copy) {
    dependsOn bootJar
    from(zipTree(tasks.bootJar.outputs.files.singleFile))
    into("build/dependency")
}

def getCurrentGitBranch() {
	if (project.hasProperty('tag')) {
		def tagValue = project.property('tag')
		return tagValue
	} else {
		def gitBranch = "Unknown branch"
		try {
			def workingDir = new File("${project.projectDir}")
			def result = 'git rev-parse --abbrev-ref HEAD'.execute(null, workingDir)
			result.waitFor()
			if (result.exitValue() == 0) {
				gitBranch = result.text.trim()
			}
		} catch (e) {
		}
		if (gitBranch == "develop" || gitBranch == "master"){
			return jar.version
		}else{
			return gitBranch
		}
	}
}

docker {
	name "${group}/${jar.baseName}:" + getCurrentGitBranch()
	copySpec.from(tasks.unpack.outputs).into("dependency")
	buildArgs(['DEPENDENCY': "dependency"])
}
